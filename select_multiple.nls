breed [sides side]     ; the four sides of the selection rectangle

sides-own [xt yt]

globals [
  ;selected ; inherited
  current-state ; "not-started", "selecting", "waiting-to-drag", "dragging"
  select-x      ; coordinates for the start of the select box
  select-y
  drag-x        ; coordinates for the start of a drag operation
  drag-y
  selected_delta ; Movements is relative to the corner of the selection box
                 ; needed so the selection box can move beyong world boundaries
]

to setup_select_multiple
  set mode "none"
  set current-state "not-started"
  set-default-shape sides "line"
  set selected_delta table:make
  set selected no-turtles
end

to select_multiple
  if mode != "select_multiple" [
    set mode "select_multiple"
    setup_select_multiple
  ]
  ; we use a different procedure depending on which state we're in
  ; the procedures will update the `current-state` to the next value as the user clicks
  (ifelse
    (current-state = "not-started")     [ start-selecting ]
    (current-state = "selecting")       [ handle-selecting ]
    (current-state = "waiting-to-drag") [ handle-waiting ]
    (current-state = "dragging")        [ handle-drag ]
  )
end

to start-selecting
  if mouse-down? [
    ; on the first click we see, we record the mouse position as the start of selection
    set select-x round mouse-xcor
    set select-y round mouse-ycor
    set current-state "selecting"
  ]
end

to handle-selecting
  ifelse mouse-down? [
    ; we keep updating the selection box as long as the mouse is down
    select select-x select-y round mouse-xcor round mouse-ycor
  ][
    ; user was selecting, but let off the mouse button
    ifelse not any? selected [
      ; if nothing was selected in the box, return to the not-started state
      deselect
      set current-state "not-started"
    ] [
      ; else we are ready to drag
      set current-state "waiting-to-drag"
    ]
  ]
  display
end

to handle-waiting
  if mouse-down? [
    ; on the first click after the selection
    ; check if we're inside or outside the box
    ifelse selected? mouse-xcor mouse-ycor [
      ; inside the box, set the starting drag coordinates
      set drag-x round mouse-xcor
      set drag-y round mouse-ycor
      set current-state "dragging"
    ] [
      ; clicked outside the select box, return to the "selecting" state
      start-selecting
    ]
  ]
end

to handle-drag
  ifelse mouse-down? [
    let new-x round mouse-xcor
    let new-y round mouse-ycor
    ; we need to move both the selected turtles and the sides
    ; of the selection rectangle by the same amount that the
    ; mouse has moved.  we do this by subtracting the current
    ; mouse coordinates from the previous mouse coordinates
    ; and adding the results to the coordinates of the turtles
    ; and sides.
    ask sides [
      ; Holds the "true" position, when it gets dragged outside the world
      set xt xt + new-x - drag-x
      set yt yt + new-y - drag-y
      ifelse is-patch? patch xt yt [
        setxy xt yt
      ] [ ; if it is outside the world, then keep it bounded
        set xcor max list min-pxcor min list max-pxcor xt
        set xcor max list min-pycor min list max-pycor yt
      ]
    ]
    let xc [xt] of min-one-of sides [xt]
    let yc [yt] of min-one-of sides [yt]
    if canMoveSelected selected xc yc [
      ask selected [
        setxy nxc xc nyc yc
      ]
    ]
    ; update the drag coordinates for the next time through
    ; if the user keeps the mouse button down
    set drag-x new-x
    set drag-y new-y
    display
  ] [
    ; user was dragging, but let off the mouse button
    ; wait for them to click again
    set current-state "waiting-to-drag"
  ]
end

to-report canMoveSelected [selected_ xc yc]
  if all? selected_ [is-patch? patch nxc xc nyc yc] [
    report not any? selected_ with [[any? turtles-here with [breed != sides]] of patch nxc xc nyc yc]
  ]
  report false
end

to-report nxc [xc]
  report (xc - item 0 (table:get selected_delta who))
end
to-report nyc [yc]
  report (yc - item 1 (table:get selected_delta who))
end

to deselect
  ask sides [ die ]
  ask selected [ set color black ]
  set selected no-turtles
  table:clear selected_delta
end

to select [x1 y1 x2 y2] ; x1 y1 is initial corner and x2 y2 is current corner
  deselect ; kill old selection rectangle
  make-side x1 y1 x2 y1
  make-side x1 y1 x1 y2
  make-side x1 y2 x2 y2
  make-side x2 y1 x2 y2
  set selected turtles with [breed != sides and selected? xcor ycor]
  ask selected [
    set color red
    table:put selected_delta who list (min list x1 x2 - xcor) (min list y1 y2 - ycor)
  ]
  show selected_delta
end

to make-side [x1 y1 x2 y2]
  ; for each side, one thin line shape is created at the mid point of each segment
  ; of the bounding box and scaled to the proper length
  create-sides 1 [
    set color gray
    setxy (x1 + x2) / 2
          (y1 + y2) / 2
    facexy x1 y1
    set xt xcor
    set yt ycor
    set size 2 * distancexy x1 y1
  ]
end

; helper procedure that determines whether a point is
; inside the selection rectangle
to-report selected? [x y]
  let y-max max [ycor] of sides  ; largest ycor is where the top is
  let y-min min [ycor] of sides  ; smallest ycor is where the bottom is
  let x-max max [xcor] of sides  ; largest xcor is where the right side is
  let x-min min [xcor] of sides  ; smallest xcor is where the left side is
  ; report whether the input coordinates are within the rectangle
  report x >= x-min and x <= x-max and
         y >= y-min and y <= y-max
end